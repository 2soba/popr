literal = @{('a' .. 'z' | 'A' .. 'Z' | '0' .. '9' | "-" | "_" | "`" | "~" | "+" | "#" | "%" | "$" | "^" | "/" | ":")+}

string = _{("'" ~ (string_value) ~ "'") | ("\"" ~ (string_value) ~ "\"") | ("\"" ~ (string_value_escape_1) ~ "\"") | ("'" ~ (string_value_escape_2) ~ "'")}
    string_escape = @{"\\"}
    string_value = @{((!("\""| string_escape | "'") ~ ANY)+)*}
    string_value_escape_1 = @{((!("\"" | string_escape) ~ ANY)+ | string_escape ~ ("\"" | string_escape))*}
    string_value_escape_2 = @{((!("'" | string_escape) ~ ANY)+ | string_escape ~ ("'" | string_escape))*}

string_list = {string ~ ("," ~ string)*}

pos_number = @{ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* | "0"}
number = @{"-"? ~ pos_number}

decimal = @{number ~ ("." ~ ASCII_DIGIT+)?}

numbers_list = {number ~ ("," ~ number)*}

step = _{":" ~ pos_number?}
numbers_range = {(full_range | right_range | all_range | left_range)}
    right_range = {":" ~ number ~ step?}
    all_range = {":" ~ step?}
    left_range = {number ~ ":"  ~ step?}
    full_range = {number ~ ":" ~ number ~ step?}

from_current = {"@" ~ root?}
from_root    = {"$" ~ root?}

select_term = _{from_current | from_root}

op = _{ge | gt | le | lt | eq | ne | re}
    ge = @{">="}
    gt = @{">"}
    le = @{"<="}
    lt = @{"<"}
    eq = @{"=="}
    ne = @{"!="}
    re = @{"=~"}

boolean = _{boolean_true | boolean_false}
    boolean_true = @{"true"}
    boolean_false = @{"false"}

term = _{select_term | decimal | string | boolean}

single_filter = {term ~ op ~ term | term}

filter_relation = _{and | or}
    and = @{"&&"}
    or  = @{"||"}

inner_filter = _{single_filter | "(" ~ filter ~ ")"}

filter = {inner_filter ~ (filter_relation ~ inner_filter)*}

all = @{"*"}

full_scan = @{".."}

square_bracket = _{"[" ~ (numbers_range | numbers_list | string_list | all | "?" ~ filter) ~ "]"}

element = _{(full_scan | ".") ~ (literal | all | square_bracket) | square_bracket}

function = _{literal ~ "(" ~ (literal)? ~ ("," ~ literal)* ~ ")"}

first_element = _{literal | all}

root = {(element|first_element) ~ (element)*}

simple_root = {"." ~ literal}

query = _{SOI ~ "$" ~ root? ~ EOI}

simple_query = _{SOI ~ "$" ~ simple_root ~ EOI}

WHITESPACE = _{ " " }